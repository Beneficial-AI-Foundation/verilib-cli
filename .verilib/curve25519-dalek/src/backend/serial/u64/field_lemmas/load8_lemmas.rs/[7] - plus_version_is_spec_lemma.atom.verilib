pub proof fn plus_version_is_spec_lemma(input: &[u8], i: usize, j: nat)
    requires
        1 <= j <= 7
    ensures
        (input[i + j] as u64) << 8 * j == pow2(j * 8) * input[i + j],
        input[i + j] * pow2(j * 8) <= u64::MAX,
        pow2(8 * (j + 1)) - 1 <= pow2(64) - 1,
        pow2(8) * pow2(8 * j) == pow2(8 * (j + 1))
{
    assert(pow2(64) - 1 == u64::MAX) by {
        lemma2_to64_rest();
    }

    assert(u8::MAX + 1 == pow2(8)) by {
        lemma2_to64();
    }

    assert(pow2(8 * (j + 1)) - 1 <= pow2(64) - 1) by {
        if (j < 7){
            lemma_pow2_strictly_increases(8 * (j + 1), 64);
        }
    }

    lemma_pow2_adds(8, j * 8);

    lemma_mul_inequality(input[i + j] as int, u8::MAX as int, pow2(j * 8) as int);

    assert((input[i + j] as u64) * pow2(j * 8) <= u64::MAX) by {
        assert(u8::MAX * pow2(j * 8) <= pow2(64) - 1) by {
            assert(
                u8::MAX * pow2(j * 8)
                ==
                (pow2(8) - 1) * pow2(j * 8)
                ==
                pow2(8 * (j + 1)) - pow2(j * 8)
            ) by {
                assert(pow2(8) >= 1) by {
                    lemma2_to64();
                }
                lemma_mul_is_distributive_sub(pow2(8) as int, 1, pow2(j * 8) as int);
            }
            assert(pow2(j * 8) > 1) by {
                lemma2_to64(); // pow2(0)
                lemma_pow2_strictly_increases(0, j * 8)
            }
        }
    }
    lemma_u64_shl_is_mul((input[i + j] as u64), (j * 8) as u64);
}